/*------- rotations.mac -----

Description of orientation.

  2021, Stanislav Mikhel
-----------------------------*/

/* X axis */
Rx(q) := block(
  [c : cos(q), s : sin(q)],
  matrix([1,0,0], [0,c,-s], [0,s,c])
)$
/* Y axis */
Ry(q) := block(
  [c : cos(q), s : sin(q)],
  matrix([c,0,s], [0,1,0], [-s,0,c])
)$
/* Z axis */
Rz(q) := block(
  [c : cos(q), s : sin(q)],
  matrix([c,-s,0], [s,c,0], [0,0,1])
)$

/* Axis - angle */
/* Represent as [theta, [kx,ky,kz]] */
Raa(k,q) := block(
  [c : cos(q), s : sin(q), v],
  v : 1 - c,
  matrix(
    [k[1]^2*v+c, k[1]*k[2]*v-k[3]*s, k[1]*k[3]*v+k[2]*s],
    [k[1]*k[2]*v+k[3]*s, k[2]^2*v+c, k[2]*k[3]*v-k[1]*s],
    [k[1]*k[3]*v-k[2]*s, k[2]*k[3]*v+k[1]*s, k[3]^2*v+c])
)$

/* Roll - pitch - yaw */
R_RPW(r,p,w) := block(
  [cr : cos(r), sr : sin(r), 
   cp : cos(p), sp : sin(p),
   cw : cos(w), sw : sin(w)],
  matrix(
    [cr*cp, -sr*cw+cr*sp*sw,  sr*sw+cr*sp*cw],
    [sr*cp,  cr*cw+sr*sp*sw, -cr*sw+sr*sp*cw],
    [  -sp,           cp*sw,           cp*cw])
)$

/* Euler angles */

R_XYZ(a,b,c) := Rx(a) . Ry(b) . Rz(c)$ 
R_XYX(a,b,c) := Rx(a) . Ry(b) . Rx(c)$
R_XZX(a,b,c) := Rx(a) . Rz(b) . Rx(c)$
R_XZY(a,b,c) := Rx(a) . Rz(b) . Ry(c)$
R_YXY(a,b,c) := Ry(a) . Rx(b) . Ry(c)$
R_YXZ(a,b,c) := Ry(a) . Rx(b) . Rz(c)$
R_YZX(a,b,c) := Ry(a) . Rz(b) . Rx(c)$
R_YZY(a,b,c) := Ry(a) . Rz(b) . Ry(c)$
R_ZXY(a,b,c) := Rz(a) . Rx(b) . Ry(c)$
R_ZXZ(a,b,c) := Rz(a) . Rx(b) . Rz(c)$
R_ZYX(a,b,c) := Rz(a) . Ry(b) . Rx(c)$  /* == R_RPW */
R_ZYZ(a,b,c) := Rz(a) . Ry(b) . Rz(c)$
