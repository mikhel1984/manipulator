/*-------- fromxml.mac ---------

Simple xml parser. Assumptions:
- file structure is correct for URDF
- 'key="value"' without spaces
- comments are between '<!' and '->'
- no comments in types and parameters 
- no nested comments

Parse into list of nodes where each node has form 
[ type, parameters, list of nodes or value ]

  2021, Stanislav Mikhel
--------------------------------*/

load(stringproc)$

/*-------------------------------
             XML
--------------------------------*/

/* check space types */
_xml_spacep(c) := member(c, [" ", "~%", "~&", "~t"])$

/* concatenate list to get string */
_xml_string(p) := apply(sconcat,p)$

/* find next nonspace element */
_xml_next(ch,strm) := if _xml_spacep(ch) then 
                        _xml_next(readchar(strm), strm) else ch$

/* Parse 'key="value"' structure 
 *   ch - current character
 *   strm - stream
 *   acc - list of found elements
 *   return [ [[k1,v1], [k2,v2], ...], last_character ] 
 */
_xml_param(ch,strm,acc) := block(
  [ k, v ],
  ch : _xml_next(ch,strm),
  if cequal(ch,"/") or cequal(ch,">") then 
    [acc, ch]           /* end of element */
  else (
    k : [], v : [],
    while not(cequal(ch,"=")) do (
      k : endcons(ch,k), 
      ch : readchar(strm)
    ),
    readchar(strm),     /* skip '="' */
    ch : readchar(strm),
    while not(cequal(ch,"\"")) do (
      v : endcons(ch,v),
      ch : readchar(strm)
    ),
    _xml_param(readchar(strm), strm, 
      cons([_xml_string(k), _xml_string(v)], acc))
  )
)$

/* Parse node type 
 *   ch - current character
 *   strm - stream
 *   return [type, last_character] 
 */
_xml_type(ch,strm) := block(
  [ t: [] ],
  while not(_xml_spacep(ch) or member(ch,["/",">"])) do (
    t : endcons(ch,t),
    ch : readchar(strm)
  ),
  [_xml_string(t), ch]
)$

/* Parse and ignore block 
 *   ch - current character
 *   strm - stream
 *   x - the penultimate symbol 
 */
_xml_ignore(ch,strm,x) := block(
  [ %1:ch ],
  ch : readchar(strm),
  while not (cequal(%1,x) and cequal(ch,">")) do (
    %1 : ch, 
    ch : readchar(strm)
  ),
  _xml_skip  /* marker */
)$

/* parse second tag */
_xml_tail(ch,strm) := block(
  while not(cequal(ch,">")) do ch : readchar(strm),
  _xml_tail_  /* marker */
)$

/* Parse text started from '<'
 *   ch - current character
 *   strm - stream
 *   In the case of node
 *   return list [ type, parameters, string or list of nodes ] 
 */
_xml_part(ch,strm) := block(
  [ %1, res ],
  if cequal(ch,"?") then 
    _xml_ignore(ch, strm, "?") /* declaration */
  elseif cequal(ch,"!") then 
    _xml_ignore(ch, strm, "-") /* comment */
  elseif cequal(ch,"/") then 
    _xml_tail(ch, strm) 
  else (
    res : [none,[],""],
    [res[1], ch] : _xml_type(ch, strm),
    [res[2], ch] : _xml_param(ch, strm, []),
    if cequal(ch,"/") then  
      readchar(strm)  /* skip '>' */
    else 
      res[3] : _xml_body(readchar(strm), strm, []),
    res
  )
)$

/* Make the list of nodes or string 
 *   ch - current character
 *   strm - stream
 *   acc - list of found elements
 *   return string of node list
 */
_xml_body(ch, strm, acc) := block(
  [ %1 : _xml_skip, s : [] ], 
  /* read text */
  while is(%1=_xml_skip) do (
    while is(ch#false) and is(ch#"<") do (
      s : endcons(ch, s),
      ch : readchar(strm)
    ),
    %1 : if charp(ch) then _xml_part(readchar(strm), strm) else _xml_tail_,
    ch : ""
  ),
  /* processing and return the result */
  if is(%1=_xml_tail_) then (
    if emptyp(acc) then _xml_string(s) else acc
  ) else 
    _xml_body(readchar(strm), strm, cons(%1, acc))
)$

/* Parse XML file 
 *   fname - name of xml file
 *   return list of nodes 
 */
xml_read(fname) := block(
  [ %1, %2 ],
  %1 : openr(fname), 
  %2 : _xml_body(readchar(%1), %1, []),
  close(%1),
  %2 
)$

/* get property by name */
_xml_property(lst,key) := block(
  [ %1 ],
  if emptyp(lst) then 
    none 
  else (
    %1 : first(lst), 
    if sequal(key,first(%1)) then second(%1) else _xml_property(rest(lst),key)
  )
)$

/* Find property value for the given key 
 *   node - node object
 *   key - key name 
 *   return value string or none 
 */
xml_property(node,key) := _xml_property(second(node),key)$

/* Get node type (tag) 
 *   node - node object
 *   return type string
 */
xml_type(node) := first(node)$ 

/* Get list of node properties (arguments) 
 *   node - node object
 *   return list of arguments
 */
xml_properties(node) := second(node)$

/* Get node content 
 *   node - node object
 *   return string of list of nodes 
 */
xml_value(node) := third(node)$

/* find node in list by type */
_xml_find(lst,tp) := block(
  [ %1 ],
  if emptyp(lst) then 
    none 
  else (
    %1 : first(lst),
    if sequal(xml_type(%1),tp) then %1 else _xml_find(rest(lst),tp)
  )
)$

/* Find node using type 
 *   lst - list of nodes 
 *   tp - type string 
 *   return found node or none 
 */
xml_find(lst,tp) := if listp(lst) then _xml_find(lst,tp) else none$

/*------------------------------      
             URDF    
-------------------------------*/

/* find paretn and child */
_urdf_connect(jnt,nm) := block(
  [ %1 : xml_value(jnt) ],
  %1 : xml_find(%1,nm),
  if is(%1=none) then none else xml_property(%1,"link")
)$

/* Parse URDF and sort its components:
 * add [parent, [child]] to each node 
 *   fname - urdf file name 
 *   return sorted nodes
 */
urdf_read(fname) := block(
  [ %1, %2, %3, %4, nm, parts, N, robot ],
  robot : first(xml_read(fname)),
  parts : xml_value(robot),
  N : length(parts),
  /* prepare for sorting */
  for i : 1 thru N do (
    %1 : parts[i],
    if sequal(xml_type(%1),"joint") then 
      parts[i] : append(%1, [none,none])
    else /* link */ 
      parts[i] : append(%1, [none,[]])
  ), 
  /* sort */
  for i : 1 thru N do (
    %1 : parts[i],
    if sequal(xml_type(%1),"joint") then (
      %2 : _urdf_connect(%1,"parent"),
      %3 : _urdf_connect(%1,"child"),
      for j : 1 thru N do (
        %4 : parts[j],
        if sequal(xml_type(%4),"link") then (
          nm : xml_property(%4, "name"),
          if is(nm=%2) then (
            /* parent */
            %1[4] : j, %4[5] : cons(i,%4[5]) ) 
          elseif is(nm=%3) then (
            /* child */
            %1[5] : j, %4[4] : i )
        )
      )
    )
  ),
  robot
)$


